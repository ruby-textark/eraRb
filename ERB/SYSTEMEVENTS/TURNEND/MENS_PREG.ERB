
; 유의사항
; TALENT:임신 은 유저에게 표시해 주기 위한 용도로만 사용
; 실제 계산에서는 BASE:임신일수 를 활용할것
; 실제로 임신한 지 모르다 뒤늦게 알아차리게 되는 전개가 마음에 들었기 떄문

@mensAndPreg
#dim dynamic pregType

	; 생리주기 계산
	MAXBASE:MASTER:생리주기 = LIMIT(MAXBASE:MASTER:생리주기, 8, 35)
	; 임신중이 아닐경우 생리주기 카운터 증가
	if !BASE:MASTER:임신일수
		BASE:MASTER:생리주기 = (BASE:MASTER:생리주기 + 1) % MAXBASE:MASTER:생리주기
	endif
	
	if TALENT:MASTER:생리불순
		BASE:MASTER:생리주기 = MAXBASE:MASTER:생리주기 / 4
		TALENT:MASTER:생리중 = 0
	endif
	
	if BASE:MASTER:생리주기 == 0
		printFormW %kr(CALLNAME:MASTER, "은", "는")% 아랫배에 위화감을 느꼈다...
		printFormW 아무래도 생리가 시작된 모양이다
		TALENT:MASTER:생리중 = 1
	elseif TALENT:MASTER:생리중 && ((BASE:MASTER:생리주기 == MAXBASE:MASTER:생리주기 / 4 - 1) || (BASE:MASTER:생리주기 < MAXBASE:MASTER:생리주기 / 4 - 1 && RAND:6 == 0))
		printFormW %kr(CALLNAME:MASTER, "은", "는")% 훨씬 상쾌한 기분이다
		printFormW %CALLNAME:MASTER%의 생리가 끝났다
		TALENT:MASTER:생리중 = 0
	endif

	if !(BASE:MASTER:임신일수 || TALENT:MASTER:생리중) && (MAXBASE:MASTER:생리주기 / 2 - 4) <= BASE:MASTER:생리주기 && BASE:MASTER:생리주기 <= (MAXBASE:MASTER:생리주기 / 2 + 1)
		TALENT:MASTER:가임기 = 1
	else
		TALENT:MASTER:가임기 = 0
	endif

	; 자궁 크기에 맞춰 오늘 담은 정액을 흘림
	BASE:MASTER:자궁내정액1 = LIMIT(BASE:MASTER:자궁내정액1, 0, BASE:MASTER:자궁크기)
	
	; 임신판정
	if !BASE:MASTER:임신일수 && (TALENT:MASTER:가임기 || FLAG:자궁대여료)
		; 자궁이 가득차있을 경우 100% 기준으로 난수 돌려서 임신판정
		if FLAG:자궁대여료 || (RAND:100 < 100 * (BASE:MASTER:자궁내정액1 + BASE:MASTER:자궁내정액2 + BASE:MASTER:자궁내정액3 + BASE:MASTER:자궁내정액4) / (BASE:MASTER:자궁크기 * 15 / 8))
			; 임신처리
			BASE:MASTER:임신일수 = 1
			
			if RAND:(BASE:MASTER:자궁내인간정액+BASE:MASTER:자궁내몬스터정액+BASE:MASTER:자궁내벌레정액+BASE:MASTER:자궁내짐승정액) < BASE:MASTER:자궁내인간정액
				; 인간임신
				CFLAG:MASTER:아이아버지 = 1
			elseif RAND:(BASE:MASTER:자궁내몬스터정액+BASE:MASTER:자궁내벌레정액+BASE:MASTER:자궁내짐승정액) < BASE:MASTER:자궁내몬스터정액
				; 몬스터임신
				CFLAG:MASTER:아이아버지 = 2
			elseif RAND:(BASE:MASTER:자궁내벌레정액+BASE:MASTER:자궁내짐승정액) < BASE:MASTER:자궁내벌레정액
				; 벌레임신
				CFLAG:MASTER:아이아버지 = 3
			else
				; 짐승임신
				CFLAG:MASTER:아이아버지 = 4
			endif
			
			if CFLAG:MASTER:아이아버지 == 1
				BASE:MASTER:자궁침식도 -= 1 + RAND:4
			else
				BASE:MASTER:자궁침식도 += 1 + RAND:4
			endif
			BASE:MASTER:자궁침식도 = LIMIT(BASE:MASTER:자궁침식도, 0, MAXBASE:MASTER:자궁침식도)
		endif
	endif
	
	; 사후피임약 복용시 24-72시간 이내일 경우 95% / 80% / 50% 확률로 피임
	; talent 있을경우 이미 확정되었으므로 돌이킬 수 없음
	if RAND:20 < 19 && (DAY == CFLAG:MASTER:사후피임약복용 + 1) && !TALENT:MASTER:임신
		BASE:MASTER:임신일수 = 0
	elseif RAND:5 < 4 && (DAY == CFLAG:MASTER:사후피임약복용 + 2) && !TALENT:MASTER:임신
		BASE:MASTER:임신일수 = 0
	elseif RAND:2 == 0 && (DAY == CFLAG:MASTER:사후피임약복용 + 3) && !TALENT:MASTER:임신
		BASE:MASTER:임신일수 = 0
	endif
	
	; 정액 하루씩 묵히기
	BASE:MASTER:자궁내정액4 = BASE:MASTER:자궁내정액3 / 2
	BASE:MASTER:자궁내정액3 = BASE:MASTER:자궁내정액2 / 2
	BASE:MASTER:자궁내정액2 = BASE:MASTER:자궁내정액1 / 2
	BASE:MASTER:자궁내정액1 = 0
	
	BASE:MASTER:자궁내인간정액 = BASE:MASTER:자궁내인간정액 / 2
	BASE:MASTER:자궁내몬스터정액 = BASE:MASTER:자궁내몬스터정액 / 2
	BASE:MASTER:자궁내벌레정액 = BASE:MASTER:자궁내벌레정액 / 2
	
	; 몬스터 혹은 벌레 정액이 인간 정액보다 많을 경우 침식도 증가
	if BASE:MASTER:자궁내몬스터정액 + BASE:MASTER:자궁내벌레정액 > BASE:MASTER:자궁내인간정액
		BASE:MASTER:자궁침식도 += 1 + RAND:2
	; 반대면 감소
	elseif BASE:MASTER:자궁내인간정액 > BASE:MASTER:자궁내몬스터정액 + BASE:MASTER:자궁내벌레정액
		BASE:MASTER:자궁침식도 -= 1 + RAND:2
	endif
	BASE:MASTER:자궁침식도 = LIMIT(BASE:MASTER:자궁침식도, 0, MAXBASE:MASTER:자궁침식도)

	; 임신시 일수 증가
	if BASE:MASTER:임신일수
		BASE:MASTER:임신일수++
	endif

	; 임신자각
	if BASE:MASTER:임신일수 == MAXBASE:MASTER:임신일수 / 10
		; 임신자각처리
		printFormW %kr(CALLNAME:MASTER, "은", "는")% 최근 생리주기가 이상한 것을 알아챘다...
		printFormW 아무래도 %kr(CALLNAME:MASTER, "은", "는")% 임신한 모양이다

		TALENT:MASTER:임신 = 1
		EXP:MASTER:임신경험++
		if CFLAG:MASTER:아이아버지 > 1
			EXP:MASTER:이종임신경험++
		endif
	endif

	; 임신중일 경우
	if BASE:MASTER:임신일수 > 0
		; 점차 자궁크기 증가
		BASE:MASTER:자궁크기 = LIMIT(BASE:MASTER:자궁크기, MAXBASE:MASTER:자궁크기 * BASE:MASTER:임신일수 / MAXBASE:MASTER:임신일수, MAXBASE:MASTER:자궁크기)
		; 점차 모유최대량 증가
		; 모유체질일 경우 최대량 제한 없어짐
		if TALENT:MASTER:모유체질
			MAXBASE:MASTER:모유 = MAXBASE:MASTER:모유 + 10 + RAND:20
		else
			MAXBASE:MASTER:모유 = LIMIT(MAXBASE:MASTER:모유 + 10 + RAND:20, 0, 1500)
		endif
	endif

	; 임신중이 아닐경우
	if BASE:MASTER:임신일수 == 0
		; 점차 자궁크기 감소
		BASE:MASTER:자궁크기 = LIMIT(BASE:MASTER:자궁크기 - ((10000 * (BASE:MASTER:자궁크기 / MAXBASE:MASTER:자궁크기)) + RAND:100), 100, MAXBASE:MASTER:자궁크기)
		; 점차 모유최대량 감소
		; 단 모유체질일 경우 감소하지 않음
		if !TALENT:MASTER:모유체질
			MAXBASE:MASTER:모유 = LIMIT(MAXBASE:MASTER:모유 - (5 + RAND:10), 0, 1500)
		endif
	endif

	if TALENT:MASTER:임신 && BASE:MASTER:임신일수 == MAXBASE:MASTER:임신일수 / 5
		if CFLAG:MASTER:아이아버지 == 1 && RAND:(MAXBASE:MASTER:자궁침식도) < BASE:MASTER:자궁침식도
			; 인간임신실패 - 낙태
			; 자궁침식도 감소
			call colorPrintW(0xcc0000, @"%kr(CALLNAME:MASTER, "은", "는")% 배에서 격통을 느꼈다")
			printFormW 이미 이종의 정액에 수 차례 범해진 %CALLNAME:MASTER%의 %kr(bodyPartStr(116), "은", "는")% 사람의 아이를 무의식중에 거부해버렸다...
			call colorPrintW(0xcc0000, @"%kr(CALLNAME:MASTER, "은", "는")% 낙태하여 [임신]을 잃었다")

			call abort
		elseif CFLAG:MASTER:아이아버지 > 1 && RAND:(MAXBASE:MASTER:자궁침식도) < MAXBASE:MASTER:자궁침식도 - BASE:MASTER:자궁침식도
			; 이종임신실패 - 낙태
			; 자궁침식도 증가
			call colorPrintW(0xcc0000, @"%kr(CALLNAME:MASTER, "은", "는")% 갑자기 다리 사이로 대량의 피를 흘렸다")
			printFormW %CALLNAME:MASTER%의 %kr(bodyPartStr(116), "은", "는")% 아직 사림이기를 포기하지 않은 듯 안간힘을 쓰며 이종의 아이를 쫓아내버렸다...
			call colorPrintW(0xcc0000, @"%kr(CALLNAME:MASTER, "은", "는")% 낙태하여 [임신]을 잃었다")

			call abort
		endif
	endif

	if BASE:MASTER:임신일수 == MAXBASE:MASTER:임신일수
		; todo 출산
		TALENT:MASTER:임신 = 0
		BASE:MASTER:임신일수 = 0
		BASE:MASTER:생리주기 = MAXBASE:MASTER:생리주기 / 2 + 1
		EXP:MASTER:출산경험++

		; todo 자궁대여 청산
		if FLAG:자궁대여료
			MONEY += FLAG:자궁대여료
			FLAG:자궁대여료 = 0
		endif
	endif

@abort

	TALENT:MASTER:임신 = 0
	BASE:MASTER:임신일수 = 0
	EXP:MASTER:낙태경험++

	; todo 자궁대여 청산
	if FLAG:자궁대여료
		FLAG:자궁대여료 = 0

		printW 낙태하였기 떄문에, 자동적으로 자궁 대여 계약은 파기되었습니다.
		call colorPrintW(0xd0936d, "「계약은 계약이지. 위약금을 물지 않은 것을 다행으로 여기게나.」")
	endif
