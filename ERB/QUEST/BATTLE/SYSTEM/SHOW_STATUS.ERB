@SHOW_STATUS

	barstr BASE:MASTER:체력, MAXBASE:MASTER:체력, 10
	printForm %CALLNAME:MASTER%의 체력 : %results% ({BASE:MASTER:체력}/{MAXBASE:MASTER:체력})
	barstr BASE:MASTER:마력, MAX(MAXBASE:MASTER:마력, 1), 10
	printFormL /마력 : %results% ({BASE:MASTER:마력}/{MAXBASE:MASTER:마력})

	barstr BASE:TARGET:체력, MAXBASE:TARGET:체력, 10
	printForm %CALLNAME:TARGET%의 체력 : %results% ({BASE:TARGET:체력}/{MAXBASE:TARGET:체력})
	barstr BASE:TARGET:마력, MAX(MAXBASE:TARGET:마력, 1), 10
	printFormL /마력 : %results% ({BASE:TARGET:마력}/{MAXBASE:TARGET:마력})
	
	drawLine
	
	call showPosition
	
	drawLine

@showPosition()

	for i, -2, 3
		for j, -2, 3
			if TCVAR:MASTER:좌우 == j && TCVAR:MASTER:상하 == i
				if TCVAR:TARGET:좌우 == j && TCVAR:TARGET:상하 == i
					setColor 0xcccc00
				else
					setColor 0x00cc00
				endif
			elseif TCVAR:TARGET:좌우 == j && TCVAR:TARGET:상하 == i
				setColor 0xcc0000
			endif
			
			if TCVAR:MASTER:좌우 == j && TCVAR:MASTER:상하 == i
				if TCVAR:TARGET:좌우 == j && TCVAR:TARGET:상하 == i
					print ※
				else
					print ★
				endif
			elseif TCVAR:MASTER:좌우 - 1 == j && TCVAR:MASTER:상하 == i
				printButton "←", 1001
			elseif TCVAR:MASTER:좌우 + 1 == j && TCVAR:MASTER:상하 == i
				printButton "→", 1002
			elseif TCVAR:MASTER:좌우 == j && TCVAR:MASTER:상하 - 1 == i
				printButton "↑", 1003
			elseif TCVAR:MASTER:좌우 == j && TCVAR:MASTER:상하 + 1 == i
				printButton "↓", 1004
			elseif TCVAR:TARGET:좌우 == j && TCVAR:TARGET:상하 == i
				print ★
			else
				print □
			endif

			resetColor
		next
		printL
	next

	call colorPrint(0x00cc00, @"★ %CALLNAME:MASTER% ")
	call colorPrint(0xcc0000, @"★ %CALLNAME:TARGET% ")
	call colorPrint(0xcccc00, "※ 교전중")
	printL
