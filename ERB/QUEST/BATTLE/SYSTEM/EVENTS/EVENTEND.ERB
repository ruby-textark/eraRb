@EVENTEND
#dim dynamic partIdx
#dim dynamic mobDrop

	FLAG:전투중 = 0
	TFLAG:이번턴성공격 = 0

	; 삽입상태초기화
	for partIdx, 101, 200
		call outFromMaster(partIdx, 1)
	next
	
	EXP:MASTER:전투경험++

	; killed the monster
	if BASE:TARGET:체력 <= 0
	
		EXP:MASTER:전투승리경험++
		CFLAG:MASTER:전투결과++
		call finishQuest
		
		; Looting
		MONEY += CFLAG:TARGET:드랍골드최소
		if CFLAG:TARGET:드랍골드최소 != CFLAG:TARGET:드랍골드최대
			MONEY += RAND:(CFLAG:TARGET:드랍골드최대 - CFLAG:TARGET:드랍골드최소)
		endif
		
		mobDrop = 1 + RAND:3
		if mobDrop == 1 && CFLAG:TARGET:드랍재료1 > 0
			ITEM:(CFLAG:TARGET:드랍재료1) += 1
		elseif mobDrop == 2 && CFLAG:TARGET:드랍재료2 > 0
			ITEM:(CFLAG:TARGET:드랍재료2) += 1
		elseif mobDrop == 3 && CFLAG:TARGET:드랍재료3 > 0
			ITEM:(CFLAG:TARGET:드랍재료3) += 1
		endif
		
		; Remove the monster
		call delMonster

	; 기생
	elseif CFLAG:TARGET:기생성공
		printFormL %CALLNAME:TARGET% %kr(NAME:TARGET, "은", "는")% %CALLNAME:MASTER%에게 기생해버렸습니다...
		printFormL 토벌은 자동으로 취소됩니다.
		
		EXP:MASTER:전투패배경험++
		CFLAG:MASTER:전투결과++
		call cancelQuest
	; Quest fail
	else
		EXP:MASTER:전투패배경험++
		CFLAG:MASTER:전투결과--

		; Check if cancel this quest or keep going with the wounded monster.
		printFormL 이번에는 실패했지만, 다음에는 충분히 준비해서 다시 도전하도록 합시다.
	endif
	
	CFLAG:MASTER:전투결과 = LIMIT(CFLAG:MASTER:전투결과, -10, 10)

	call statsUpgrade
	call afterAction

	begin shop
