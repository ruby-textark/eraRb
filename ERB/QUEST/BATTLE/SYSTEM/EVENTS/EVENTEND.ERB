@EVENTEND
#dim dynamic partIdx
#dim dynamic mobDrop

	FLAG:전투중 = 0
	TFLAG:이번턴성공격 = 0

	; 삽입상태초기화
	for partIdx, 101, 200
		call outFromMaster(partIdx, 1)
	next
	
	EXP:MASTER:전투경험++

	; killed the monster
	if BASE:TARGET:체력 <= 0
	
		EXP:MASTER:전투승리경험++
		CFLAG:MASTER:전투결과++
		
		if CFLAG:TARGET:캐릭터번호 == FLAG:퀘스트대상
			call stepQuest
		endif
		
		; Looting
		mobDrop = CFLAG:TARGET:드랍골드최소
		if CFLAG:TARGET:드랍골드최소 > CFLAG:TARGET:드랍골드최대
			mobDrop += RAND:(CFLAG:TARGET:드랍골드최대 - CFLAG:TARGET:드랍골드최소)
		endif

		printFormL 돈을 {mobDrop}원 주웠다
		MONEY += mobDrop
		
		mobDrop = CFLAG:TARGET:드랍마석최소
		if CFLAG:TARGET:드랍마석최대 > CFLAG:TARGET:드랍마석최소
			mobDrop += RAND:(CFLAG:TARGET:드랍마석최대 - CFLAG:TARGET:드랍마석최소)
		endif

		mobDrop = mobDrop * FLAG:계약배율 / 100
		printFormL 마석을 {mobDrop}개 주웠다
		FLAG:마석 += mobDrop
		
		mobDrop = 1 + RAND:3
		if mobDrop == 1 && CFLAG:TARGET:드랍재료1 > 0
			ITEM:(CFLAG:TARGET:드랍재료1) += 1
			printFormL %kr(ITEMNAME:(CFLAG:TARGET:드랍재료1), "을", "를")% 얻었다
		elseif mobDrop == 2 && CFLAG:TARGET:드랍재료2 > 0
			ITEM:(CFLAG:TARGET:드랍재료2) += 1
			printFormL %kr(ITEMNAME:(CFLAG:TARGET:드랍재료2), "을", "를")% 얻었다
		elseif mobDrop == 3 && CFLAG:TARGET:드랍재료3 > 0
			ITEM:(CFLAG:TARGET:드랍재료3) += 1
			printFormL %kr(ITEMNAME:(CFLAG:TARGET:드랍재료3), "을", "를")% 얻었다
		endif

	; 기생
	elseif CFLAG:TARGET:기생성공
		printFormL %CALLNAME:TARGET% %kr(NAME:TARGET, "은", "는")% %CALLNAME:MASTER%에게 기생해버렸습니다...
		
		EXP:MASTER:전투패배경험++
	; 패배
	else
		EXP:MASTER:전투패배경험++
		CFLAG:MASTER:전투결과--

		; Check if cancel this quest or keep going with the wounded monster.
		printFormL %kr(NAME:TARGET, "과의", "와의")% 전투에서 힘이 다한 %kr(NAME:MASTER, "은", "는")% 눈앞이 깜깜해졌다...
		call banishFromLabyrinth
	endif

	; Remove the monster
	call delMonster
	
	CFLAG:MASTER:전투결과 = LIMIT(CFLAG:MASTER:전투결과, -10, 10)

	call statsUpgrade

	begin shop
