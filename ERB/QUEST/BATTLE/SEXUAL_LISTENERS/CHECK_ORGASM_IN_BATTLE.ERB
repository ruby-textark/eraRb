@checkOrgasmInBattle
#dim dynamic orgasmThres
#dim dynamic orgasmCount
#dim dynamic orgasmParts, 100
#dim dynamic insuffOrgasm

	orgasmThres = masterOrgasmThres()
	for i, 101, 200
		if PALAM:MASTER:i >= orgasmThres
			; todo 절정구상
			printFormL %STR:(i)% 절정
			orgasmParts:(orgasmCount++) = i - 400
			DOWN:i = PALAM:MASTER:i - PALAM:MASTER:i % orgasmThres

			; 욕정 증가
			; 약절정
			if PALAM:MASTER:i / orgasmThres == 1
				TIMES UP:욕정, 1.25
			; 2배절정
			elseif PALAM:MASTER:i / orgasmThres == 2
				TIMES UP:욕정, 1.5
			; 3배절정
			elseif PALAM:MASTER:i / orgasmThres == 3
				TIMES UP:욕정, 2
			; 그외
			elseif PALAM:MASTER:i / orgasmThres > 3
				TIMES UP:욕정, 3
			endif

			; 절정경험 증가
			EXP:MASTER:절정경험 += PALAM:MASTER:i / orgasmThres
			EXP:MASTER:(i + 100) += PALAM:MASTER:i / orgasmThres
		elseif PALAM:MASTER:i >= 10000
			; 절정봉인 적용중
			insuffOrgasm++
		endif
	next

	if orgasmCount
		if ABL:TARGET:흡정
			printFormL %kr(CALLNAME:MASTER, "은", "는")% %CALLNAME:TARGET%에게 착정당하고 있다

			; 최대 마력의 5%를 강탈 / 최대 15%
			if BASE:MASTER:마력 >= MIN(orgasmCount, 3) * MAXBASE:MASTER:마력 / 20
				DOWNBASE:MASTER:마력 += MIN(orgasmCount, 3) * MAXBASE:MASTER:마력 / 20
			; 무족하면 패널티
			; 최대 체력의 2% / 최대 6%
			else
				printFormL 하지만 %CALLNAME:MASTER%의 마력은 이미 바닥이었고, 대신해서 체력을 흡수당했다

				DOWNBASE:MASTER:마력 = BASE:MASTER:마력
				DOWNBASE:MASTER:체력 = MIN(orgasmCount, 3) * MAXBASE:MASTER:체력 / 50
			endif
		endif
		; todo 절정구상
		call onMasterOrgasm_m(orgasmParts, orgasmCount, 1)
		call onMasterOrgasm_e(orgasmParts, orgasmCount, 1)
	elseif insuffOrgasm
		printFormL %kr(CALLNAME:MASTER, "은", "는")% 절정을 봉인당한 상태다
		call onMasterInsuffOrgasm_m(1)
		call onMasterInsuffOrgasm_e(1)
	endif
