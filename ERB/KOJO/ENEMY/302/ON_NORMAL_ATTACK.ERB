; 죽음의 기사의 패턴용 변수
; TCVAR:TARGET:10 = 대사용(사용x)
; TCVAR:TARGET:11 = 돌진
; TCVAR:TARGET:12 = 투혼
@c302onNormalAttack
#dim dynamic damageBase

	; todo 죽음의 기사 패턴 작업
	if BASE:TARGET:체력 < MAXBASE:TARGET:체력 * 2 / 3 && TCVAR:TARGET:11 == 0
		call speak(TARGET, "각오하게.")
		printFormL %kr(NAME:TARGET, "이", "가")% %kr(NAME:MASTER, "을", "를")% 노려본다.
		TCVAR:TARGET:11 = 1
	elseif TCVAR:TARGET:11 == 1
		printFormL %NAME:TARGET%의 필멸의 돌진
		
		TCVAR:TARGET:상하 = TCVAR:MASTER:상하
		TCVAR:TARGET:좌우 = TCVAR:MASTER:좌우

		damageBase = 10 * MAX(1, getActualAtk(TARGET) - getActualDef(MASTER) / 2)
		LOSEBASE:체력 = damageBase * 9 / 10 + RAND:(damageBase / 10)

		TCVAR:TARGET:11 = 2
	
	elseif BASE:TARGET:체력 < MAXBASE:TARGET:체력 / 3 && TCVAR:TARGET:12 == 0
		call speak(TARGET, "훌륭하군.")
		printFormL %kr(NAME:TARGET, "이", "가")% 검은 마력을 퍼뜨린다.
		printFormL %NAME:TARGET%의 투혼
		
		call applyBuffTo(TARGET, "투혼", 100)
		
		TCVAR:TARGET:12 = 1
	
	else
		printFormL %NAME:TARGET%의 공격
		damageBase = 10 * MAX(1, getActualAtk(TARGET) - getActualDef(MASTER))
		LOSEBASE:체력 = damageBase * 9 / 10 + RAND:(damageBase / 10)
	endif
