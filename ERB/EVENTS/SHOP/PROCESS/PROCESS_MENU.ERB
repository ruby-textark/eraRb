@processMenu

	; This commands are always OK
	if RESULT != 201 && RESULT != 202 && RESULT < 800 || RESULT == 901
		if !isMasterFree()
			return 0
		endif
	endif

	; QUEST MENU
	if RESULT == 101 && FLAG:자궁대여료 == 0 && isMasterFree() && !FLAG:원정중
		call startExpedition
		RESULT = 0
	elseif RESULT == 102 && !hasHuntQuest() && !FLAG:원정중
		call newQuestList
	elseif RESULT == 103 && isMasterFree() && hasQuest()
		call cancelQuest
	elseif RESULT == 104 && hasDeliverQuest() && hasDeliveryItems()
		ITEM:(FLAG:퀘스트대상) -= (FLAG:퀘스트값)
		call stepQuest

	; CHARACTER MENU
	elseif RESULT == 201
		call showMasterInfo
		RESULT = 0
	elseif RESULT == 202
		call showInventory
		RESULT = 0
	elseif RESULT == 203
		call showMasterEquips
		RESULT = 0
	elseif RESULT == 204
		call showSkills
		RESULT = 0

	; SHOP MENU
	elseif RESULT == 301 && isMasterFree() && !FLAG:원정중
		call itemShop
	elseif RESULT == 302 && isMasterFree() && !FLAG:원정중
		call backStreet
	elseif RESULT == 303 && isMasterFree() && !FLAG:원정중
		call cathedral

	; SYSTEM MENU
	elseif RESULT == 801
		saveGame
		RESULT = 0
	elseif RESULT == 802
		loadGame
	elseif RESULT == 803
		call configMenu
		RESULT = 0

	; TURN END
	elseif RESULT == 900 && !FLAG:원정중
		begin turnend
	elseif RESULT == 901 && !FLAG:원정중 && MONEY >= 2000
		printFormW %kr(CALLNAME:MASTER, "은", "는")% 푹 쉬었다
		printFormW 숙박비 \\2000 차감
		
		MONEY -= 2000

		BASE:MASTER:체력 = MAXBASE:MASTER:체력
		BASE:MASTER:마력 = MAXBASE:MASTER:마력

		begin turnend
	; WRONG COMMAND
	else
		RESULT = 0
	endif

	return RESULT

@saveInfo
	PUTFORM {DAY,3,RIGHT}일차 %NAME:MASTER, 14, LEFT%
