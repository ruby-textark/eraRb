@relax
#dim dynamic mental
#dim dynamic regenAmount

	regenAmount = MAXBASE:MASTER:체력 / 10
	if FLAG:원정중 && FLAG:탈진의계약
		repeat FLAG:탈진의계약
			regenAmount -= regenAmount / 10
		rend
	endif
	BASE:MASTER:체력 = LIMIT(BASE:MASTER:체력 + regenAmount, 0, MAXBASE:MASTER:체력)
	
	regenAmount = MAXBASE:MASTER:마력 / 20
	if FLAG:원정중 && FLAG:탈진의계약
		repeat FLAG:탈진의계약
			regenAmount -= regenAmount / 10
		rend
	endif
	BASE:MASTER:마력 = LIMIT(BASE:MASTER:마력 + regenAmount, 0, MAXBASE:MASTER:마력)

	mental = 100 + ((PALAM:MASTER:순종 + PALAM:MASTER:만족 + PALAM:MASTER:욕정) - (PALAM:MASTER:공포 + PALAM:MASTER:불쾌 + PALAM:MASTER:억울))
	mental = LIMIT(mental, -500, 500)
	
	if !TALENT:MASTER:조울증 && (mental == 500 || mental == -500) 
		printFormW %kr(CALLNAME:MASTER, "은", "는")% 급격한 감정의 변화로 마음이 불안해졌다
		call getTalent("조울증")
	endif

	BASE:MASTER:이성 = LIMIT(BASE:MASTER:이성 + mental, 0, MAXBASE:MASTER:이성)

	BASE:MASTER:모유 = MAXBASE:MASTER:모유

	call relaxMasturbation
	call relaxPalams
	call relaxDiets

@relaxMasturbation
#dim dynamic partIdx

	for partIdx, 101, 200
		if !isOrdinaryPart(partIdx)
			continue
		endif
		
		if PALAM:MASTER:partIdx > masterOrgasmThres() / 2
			callf pushShuffle(partIdx, 1)
		endif
	next
	
	if isShuffleEmpty()
		return
	endif
	
	partIdx = popShuffle()
	
	if RAND:3 == 0
		printFormW %kr(CALLNAME:MASTER, "은", "는")% %kr(partStrRaw(partIdx), "이", "가")% 달아올라 자위를 시작했다
	elseif RAND:2 == 0
		if isHole(partIdx)
			printFormW 해소되지 않은 욕망에 패배한 %kr(CALLNAME:MASTER, "은", "는")% %kr(partStrRaw(partIdx), "을", "를")% 쑤시며 자위를 시작했다
		else
			printFormW %kr(CALLNAME:MASTER, "은", "는")% 몽글몽글하게 퍼져나가는 쾌감에 빠져 %kr(partStrRaw(partIdx), "을", "를")% 만지작거리며 스스로를 위로하기 시작했다
		endif
	else
		printFormW 인내심이 떨어진 %kr(CALLNAME:MASTER, "은", "는")% %kr(partStrRaw(partIdx), "으로", "로")% 자위하기 시작했다
	endif

	call onMasterMasturbation(partIdx)

@relaxPalams
#dim dynamic palamIdx

	for palamIdx, 0, varSize("PALAM")
		PALAM:MASTER:palamIdx /= 4
		
		if PALAM:MASTER:palamIdx < 100
			PALAM:MASTER:palamIdx = 0
		endif
	next

@relaxDiets
#dim dynamic partIdx
	
	; 부적절한 식습관은 몸에 좋지 않습니다
	if BASE:MASTER:영양상태 < MAXBASE:MASTER:영양상태 - 500 || BASE:MASTER:영양상태 > MAXBASE:MASTER:영양상태 + 500
		for partIdx, 101, 200			
			if strlens(BASENAME:partIdx) == 0
				continue
			endif
			
			BASE:MASTER:partIdx -= RAND:2
		next
		
		if BASE:MASTER:영양상태 < MAXBASE:MASTER:영양상태 - 500
			BASE:MASTER:가슴둘레 -= RAND:((MAXBASE:MASTER:영양상태 - BASE:MASTER:영양상태) / 300)
			BASE:MASTER:가슴크기 -= RAND:((MAXBASE:MASTER:영양상태 - BASE:MASTER:영양상태) / 400)
			BASE:MASTER:허리둘레 -= RAND:((MAXBASE:MASTER:영양상태 - BASE:MASTER:영양상태) / 200)
			BASE:MASTER:엉덩이둘레 -= RAND:((MAXBASE:MASTER:영양상태 - BASE:MASTER:영양상태) / 250)
		else
			BASE:MASTER:가슴둘레 += RAND:((BASE:MASTER:영양상태 - MAXBASE:MASTER:영양상태) / 300)
			BASE:MASTER:가슴크기 += RAND:((BASE:MASTER:영양상태 - MAXBASE:MASTER:영양상태) / 400)
			BASE:MASTER:허리둘레 += RAND:((BASE:MASTER:영양상태 - MAXBASE:MASTER:영양상태) / 200)
			BASE:MASTER:엉덩이둘레 += RAND:((BASE:MASTER:영양상태 - MAXBASE:MASTER:영양상태) / 250)
		endif

	; 잘 먹고 휴식하면 몸에 좋습니다
	else
		for partIdx, 101, 200			
			if strlens(BASENAME:partIdx) == 0
				continue
			endif
			
			BASE:MASTER:partIdx += RAND:2
		next
	endif

	; 기초대사량 재계산
	; todo 활동대사량도 넣을것
	MAXBASE:MASTER:영양상태 = getMasterMetabolicRate()
	BASE:MASTER:영양상태 = 0
