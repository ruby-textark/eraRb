@showInventory
#dim dynamic itemIdx
#dim dynamic showCount
#dim dynamic selectedItem
#dim dynamic isItemUsable
#dim dynamic isItemEquipable

$inputLoop
	clearLine 1
	printL =인벤토리=
	printL 사용할 아이템을 선택해주세요

	showCount = 0
	for itemIdx, 1, 1500
	
		if !ITEM:itemIdx
			continue
		endif
	
		; No such item
		if strlens(ITEMNAME:itemIdx) == 0
			continue
		endif

		tryccallform isUsable{itemIdx}(MASTER)
			isItemUsable = RESULT
		catch
			isItemUsable = 0
		endcatch
		
		if !isItemUsable
			call colorPrint(0x888888, @"[{itemIdx, 4, RIGHT}] ")
		else
			printForm [{itemIdx, 4, RIGHT}]%" "%
		endif

		printFormLC %ITEMNAME:itemIdx, 14, RIGHT%x{ITEM:itemIdx, 3, LEFT}
		showCount++
		if showCount % 4 == 0
			printL
		endif
		
		resetColor
	
	next

	printL
	printL [0] - 돌아가기

	input
	selectedItem = RESULT
	
	tryccallform isUsable{selectedItem}(MASTER)
		isItemUsable = RESULT
	catch
		isItemUsable = 0
	endcatch

	if ITEM:selectedItem > 0 && isItemUsable
		tryccallform onUseItem{selectedItem}(MASTER)
			ITEM:selectedItem--
		catch
		endcatch
	elseif selectedItem == 0
		return
	endif

	goto inputLoop
