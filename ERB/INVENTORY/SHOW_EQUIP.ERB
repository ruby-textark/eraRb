; 어차피 주인공 이외의 대상에 대해 콜하지는 않긴 한데 이런저런 래퍼가 필요하긴 함
@showMasterEquips
#dim dynamic equipIdx
#dim dynamic equipSlotIdx
$wrapper

	call showCharaEquips
	
	printL [900] - 돌아가기
	input
	equipSlotIdx = RESULT
	
	if 11 <= equipSlotIdx && equipSlotIdx <= 19
		if strlens(EQUIPNAME:equipSlotIdx) == 0
			goto wrapper
		endif

		printFormL %EQUIPNAME:equipSlotIdx% 장비 목록
		printL 교체할 장비를 아래에서 선택해주세요
		drawLine
		
		for equipIdx, 1001 + (equipSlotIdx - 10) * 100, 1100 + (equipSlotIdx - 10) * 100
			if ITEM:equipIdx && EQUIP:MASTER:equipSlotIdx != equipIdx
				printFormL [{equipIdx}] - %ITEMNAME:equipIdx%
			endif
		next
		
		print [1000] - 

		if EQUIP:MASTER:equipSlotIdx == 0
			printL 돌아가기
		else
			printL 해제
		endif

		input
		
		if RESULT == 1000
			if EQUIP:MASTER:equipSlotIdx == 0
				RESULT = 1
			else
				call unequipMasterEquips(equipSlotIdx)
			endif
		else
			call changeMasterEquips(equipSlotIdx, RESULT)
		endif
	endif

	if RESULT != 900
		goto wrapper
	endif

	return 1

@showCharaEquips
#dim dynamic equipSlotIdx

	clearLine 99999
	
	printFormL %NAME:MASTER%의 현재 장비 상태
	drawLine
	
	for equipSlotIdx, 11, 19
		if strlens(EQUIPNAME:equipSlotIdx) == 0
			continue
		endif
		
		printForm [{equipSlotIdx}] %EQUIPNAME:equipSlotIdx% 장비 : 
		
		if EQUIP:MASTER:equipSlotIdx
			printFormL %ITEMNAME:(EQUIP:MASTER:equipSlotIdx)%
		else
			printFormL 없음
		endif
	next
